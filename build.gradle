import java.util.stream.Collectors

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'jacoco'

group 'com.github.vilmosnagy.elq'


task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

release {
    tagTemplate = 'release-${version}'
}

project.ext.relesedProjects = ['elq-core']
project.allprojects.forEach { p ->
    p.ext.released = project.ext.relesedProjects.any { rp -> p.name.contains(rp) }
}

if (! project.hasProperty("doNotRelease")) {
    apply from: 'maven-push.gradle'
    project.getTasksByName('uploadArchives', true).stream().filter { t -> project.getSubprojects().any {p -> project.ext.relesedProjects.any {rp -> p.name.contains(rp) } && p == t.project}}.forEach { t -> afterReleaseBuild.dependsOn t}
}